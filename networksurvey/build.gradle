apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: 'kotlin-android'


ext.androidVersion = '28.0.0'
ext.grpcVersion = '1.58.0'
ext.networkSurveyMessagingVersion = '0.15.0'
ext.navigationVersion = '2.7.6'

android {
    compileSdk 34
    defaultConfig {
        buildConfigField "String", "MESSAGING_API_VERSION", "\"${networkSurveyMessagingVersion}\""
        applicationId "com.craxiom.networksurvey"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 56
        versionName "1.18"
        setProperty("archivesBaseName", "$applicationName-$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.7'
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties', 'google/cloud/audit/audit_log.proto']
        }
    }

    lint {
        abortOnError false
    }
    namespace 'com.craxiom.networksurvey'

    flavorDimensions "version"
    productFlavors {
        // Regular is for Google Play that contains Firebase tracking features such as crashlytics
        regular {
            versionNameSuffix ""
        }

        // Has the extra CDR features and does not contain any tracking
        cdr {
            versionNameSuffix "-cdr"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "androidx.navigation:navigation-runtime-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-fragment-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-ui-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-compose:${navigationVersion}"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }

    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "io.grpc:grpc-android:${grpcVersion}"

    implementation("com.craxiom:network-survey-messaging:${networkSurveyMessagingVersion}") {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation "com.google.protobuf:protobuf-java-util:3.24.3"

    implementation "mil.nga.geopackage:geopackage-android:6.6.0"
    implementation 'org.apache.commons:commons-csv:1.10.0'

    implementation 'com.craxiom:mqtt-library:0.7.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation 'app.futured.donut:donut:2.2.3'
    implementation 'com.patrykandpatrick.vico:core:2.0.0-alpha.5'
    implementation 'com.patrykandpatrick.vico:compose-m3:2.0.0-alpha.5'

    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.2'

    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "androidx.lifecycle:lifecycle-runtime-compose"
    implementation "com.google.accompanist:accompanist-themeadapter-material:0.28.0"

    // Only include firebase in the google play build
    regularImplementation platform('com.google.firebase:firebase-bom:32.7.0')
    regularImplementation 'com.google.firebase:firebase-analytics'
    regularImplementation 'com.google.firebase:firebase-crashlytics'

    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.annotation:annotation:1.7.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'com.adevinta.android:barista:4.2.0'
    androidTestImplementation 'com.google.truth:truth:1.1.2'
    androidTestImplementation "com.craxiom:network-survey-messaging:${networkSurveyMessagingVersion}"
    androidTestImplementation "mil.nga.geopackage:geopackage-android:6.6.0"
}
